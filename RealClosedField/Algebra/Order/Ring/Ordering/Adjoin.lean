/-
Copyright (c) 2024 Florent Schaffhauser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Florent Schaffhauser, Artie Khovanov
-/
import Mathlib.Order.Zorn
import RealClosedField.Algebra.Order.Ring.Ordering.Basic

/-!

Let `R` be a commutative ring, and let `P` be a preordering on `R`. If `a ∉ P`, then we can extend
`P` to a preordering containing either `a` or `-a`.
Moreover, we can always extend `P` to an ordering on `R`.

We also prove various sufficient conditions to be able to extend `P` to `a`,
and that all orderings on a field are maximal preorderings.

## References

- [*An introduction to real algebra*, T.Y. Lam][lam_1984]

-/

variable {R : Type*} [CommRing R] (P : Subsemiring R) (a : R)

namespace Subsemiring.IsPreordering

variable (P : Subsemiring R) [P.IsPreordering] (a : R)

/-
### Sufficient conditions for adjoining an element to a preordering
-/

variable {P a} in
theorem closure_insert (hx : -1 ∉ closure (insert a P)) :
    (closure (insert a P)).IsPreordering := .of_le (P := P) (fun _ ↦ by aesop) hx

variable {P a} in
theorem mem_closure_insert {x} :
    x ∈ closure (insert a P) ↔ ∃ y ∈ P, ∃ z ∈ P, x = y + a * z where
  mp hx := by
    induction hx using Subsemiring.closure_induction with
    | mem x hx => cases hx
                  · exact ⟨0, by aesop, 1, by aesop, by simp_all⟩
                  · exact ⟨x, ‹_›, 0, by aesop, by simp⟩
    | zero => exact ⟨0, by aesop, 0, by aesop, by simp⟩
    | one => exact ⟨1, by aesop, 0, by aesop, by simp⟩
    | add _ _ _ _ ha hb =>
      rcases ha, hb with ⟨⟨x₁, hx₁, y₁, hy₁, rfl⟩, ⟨x₂, hx₂, y₂, hy₂, rfl⟩⟩
      exact ⟨x₁ + x₂, by aesop, y₁ + y₂, by aesop, by linear_combination⟩
    | mul _ _ _ _ ha hb =>
      rcases ha, hb with ⟨⟨x₁, hx₁, y₁, hy₁, rfl⟩, ⟨x₂, hx₂, y₂, hy₂, rfl⟩⟩
      exact ⟨x₁ * x₂ + (a * a) * (y₁ * y₂), by aesop, x₁ * y₂ + y₁ * x₂, by aesop,
        by linear_combination⟩
  mpr := by aesop

variable {P} in
theorem neg_one_notMem_closure_insert_or_of_neg_mul_mem {x y : R} (h : -(x * y) ∈ P) :
    -1 ∉ closure (insert x P) ∨ -1 ∉ closure (insert y P) := by
  rw [mem_closure_insert]
  by_contra
  apply neg_one_notMem P
  rcases (mem_closure_insert).mp (show -1 ∈ closure (insert x P) by aesop)
    with ⟨s₁, hs₁, s₂, hs₂, eqx⟩
  rcases (mem_closure_insert).mp (show -1 ∈ closure (insert y P) by aesop)
    with ⟨t₁, ht₁, t₂, ht₂, eqy⟩
  rw [show -1 = (-(x * y)) * s₂ * t₂ + s₁ + t₁ + (s₁ * t₁) by
    linear_combination (t₁ + 1) * eqx - 1 * x * s₂ * eqy]
  aesop (config := { enableSimp := false })

theorem neg_one_notMem_closure_insert_or :
    -1 ∉ closure (insert a P) ∨ -1 ∉ closure (insert (-a) P) :=
  neg_one_notMem_closure_insert_or_of_neg_mul_mem (by simp_all : -(a * (- a)) ∈ P)

theorem neg_one_notMem_closure_insert
    (h : ∀ x y, x ∈ P → y ∈ P → x + (1 + y) * a + 1 ≠ 0) :
    -1 ∉ closure (insert a P) := fun _ => by
  rcases (mem_closure_insert).mp
      (show -1 * (1 + a) ∈ closure (insert a P) by aesop (erase simp neg_mul))
    with ⟨x, hx, y, hy, eqn⟩
  exact h _ _ hx hy (by linear_combination - eqn)

/--
If `F` is a field, `P` is a preordering on `F`, and `a` is an element of `P` such that `-a ∉ P`,
then `-1` is not in the semiring generated by `P` and `a`.
-/
theorem neg_one_notMem_closure_insert_of_neg_notMem
    {F : Type*} [Field F] {P : Subsemiring F} [P.IsPreordering] {a : F}
    (ha : -a ∉ P) : -1 ∉ Subsemiring.closure (insert a P) := by
  rw [mem_closure_insert]
  rintro ⟨x, hx, y, hy, eqn⟩
  apply ha
  have : y ≠ 0 := fun _ => by aesop
  rw [show -a = x * y⁻¹ + y⁻¹ by field_simp; linear_combination eqn]
  aesop

/-!
### Existence of orderings
-/

theorem exists_le :
    ∃ Q ≥ P, Q.IsPreordering ∧ (a ∈ Q ∨ -a ∈ Q) := by
  cases neg_one_notMem_closure_insert_or P a with
  | inl h => exact ⟨_, fun _ ↦ by aesop, closure_insert h, by aesop⟩
  | inr h => exact ⟨_, fun _ ↦ by aesop, closure_insert h, by aesop⟩

theorem exists_lt (hp : a ∉ P) (hn : -a ∉ P) :
    ∃ Q > P, Q.IsPreordering := by
  rcases exists_le P a with ⟨Q, le, hQ, p_mem | n_mem⟩
  · exact ⟨Q, lt_of_le_of_ne le <| Ne.symm (ne_of_mem_of_not_mem' p_mem hp), hQ⟩
  · exact ⟨Q, lt_of_le_of_ne le <| Ne.symm (ne_of_mem_of_not_mem' n_mem hn), hQ⟩

end IsPreordering

/- A maximal preordering on `R` is an ordering. -/
theorem IsOrdering.of_maximal_isPreordering {O : Subsemiring R} (max : Maximal IsPreordering O) :
    O.IsOrdering :=
  have := max.prop
  isOrdering_iff.mpr <| fun a b h => by
  cases IsPreordering.neg_one_notMem_closure_insert_or_of_neg_mul_mem h with
  | inl h => exact Or.inl <| max.le_of_ge (IsPreordering.closure_insert h)
                              (fun _ ↦ by aesop) (by aesop)
  | inr h => exact Or.inr <| max.le_of_ge (IsPreordering.closure_insert h)
                              (fun _ ↦ by aesop) (by aesop)

/- Every preordering on `R` extends to an ordering. -/
theorem IsPreordering.exists_le_isOrdering (P : Subsemiring R) [P.IsPreordering] :
    ∃ O ≥ P, O.IsOrdering :=
  have := P.isSemireal_ofIsPreordering
  have ⟨_, _, hO⟩ : ∃ O, P ≤ O ∧ Maximal IsPreordering O := by
    refine zorn_le_nonempty₀ {P : Subsemiring R | IsPreordering P} ?_ P (by simpa)
    exact fun S hS hc Q hQ =>
      ⟨_, isPreordering_sSup (Set.nonempty_of_mem hQ) hc.directedOn hS, CompleteLattice.le_sSup S⟩
  ⟨_, ‹_›, .of_maximal_isPreordering hO⟩

/- An ordering on `R` is maximal among preorderings iff it is maximal among orderings. -/
theorem IsOrdering.maximal_isPreordering_iff_maximal_isOrdering
    {O : Subsemiring R} [O.IsOrdering] :
    Maximal IsPreordering O ↔ Maximal IsOrdering O where
  mp h := Maximal.mono h (fun _ _ => inferInstance) ‹_›
  mpr hO :=
    ⟨inferInstance, fun P hP h ↦ by
      rcases IsPreordering.exists_le_isOrdering P with ⟨O', hO', hO'₂⟩
      simpa [Maximal.eq_of_ge hO hO'₂ (by order)] using hO'⟩

/-!
### Comparison of orderings
-/

variable {P Q : Subsemiring R} {a : R}

def HasMemOrNegMem.of_le [HasMemOrNegMem P] (h : P ≤ Q) : HasMemOrNegMem Q where
  mem_or_neg_mem a := have := mem_or_neg_mem P a; by aesop

theorem mem_support_of_ge_of_notMem [HasMemOrNegMem P]
    (h : P ≤ Q) (ha : a ∈ Q) (haP : a ∉ P) :
    let := HasMemOrNegMem.of_le h
    a ∈ Q.support := by
  aesop (add simp mem_support)

theorem eq_of_le_of_support_eq_bot [HasMemOrNegMem P] (h : P ≤ Q)
    (hSupp : let := HasMemOrNegMem.of_le h; Q.support = ⊥) : P = Q := by
  by_contra h2
  have ⟨x, hx, hx2⟩ : ∃ x, x ∈ Q ∧ x ∉ P :=
    Set.exists_of_ssubset <| lt_of_le_of_ne h (by simpa using h2)
  have : -x ∈ Q := by aesop
  apply_fun (x ∈ ·) at hSupp
  aesop (add simp mem_support)

theorem IsOrdering.eq_of_ge {F : Type*} [Field F] {O P : Subsemiring F}
    [O.IsOrdering] [P.IsPreordering] (h : O ≤ P) : O = P :=
  eq_of_le_of_support_eq_bot h (IsPreordering.support_eq_bot P)

/- A preordering on a field `F` is maximal iff it is an ordering. -/
theorem maximal_iff_isOrdering {F : Type*} [Field F] {O : Subsemiring F} :
    Maximal IsPreordering O ↔ O.IsOrdering where
  mp h := .of_maximal_isPreordering h
  mpr _ := ⟨inferInstance, fun _ _ ge ↦ by simp [IsOrdering.eq_of_ge ge]⟩
